plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.5-SNAPSHOT" apply false
    id "me.modmuss50.mod-publish-plugin" version "0.4.5"
}

ext.isSnapshot = providers.environmentVariable("SNAPSHOT").map { it == "true" }.getOrElse(false)

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        // The following line declares the mojmap mappings, you may use other mappings as well
        mappings loom.officialMojangMappings()
        // The following line declares the yarn mappings you may select this one as well.
        // mappings "net.fabricmc:yarn:1.20.1+build.10:v2"
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    base {
        archivesName = rootProject.archives_base_name
    }

    archivesBaseName = rootProject.archives_base_name
    version = "${mod_version}+mc${minecraft_version}${isSnapshot ? '-SNAPSHOT' : ''}"
    group = rootProject.maven_group

    repositories {
        maven { url "https://maven.neomc.dev/releases" }
        maven { url "https://maven.ftb.dev/snapshots" }
        maven { url "https://maven.ftb.dev/releases" }
        maven { url 'https://dl.cloudsmith.io/public/geckolib4/geckolib/maven/' }
        maven { url "https://maven.teamresourceful.com/repository/maven-public/" }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}

publishMods {
    dryRun = providers.environmentVariable("CURSE_TOKEN").getOrNull() == null
    changelog = file("./CHANGELOG.md").text
    version = "${mod_version}"
    type = STABLE

    def fabricOptions = publishOptions {
        file = project.provider { project(":fabric").tasks.remapJar }.flatMap { it.archiveFile }
        displayName = "[Fabric - ${minecraft_version}] ${project.name} ${mod_version}"
        modLoaders.add("fabric")
    }

//    def forgeOptions = publishOptions {
//        file = project.provider { project(":forge").tasks.remapJar }.flatMap { it.archiveFile }
//        displayName = "[Forge - ${minecraft_version}] ${project.name} ${mod_version}"
//        modLoaders.add("forge")
//    }

    def neoOptions = publishOptions {
        file = project.provider { project(":neoforge").tasks.remapJar }.flatMap { it.archiveFile }
        displayName = "[NeoForge - ${minecraft_version}] ${project.name} ${mod_version}"
        modLoaders.add("neoforge")
    }

    def curseForgeOptions = curseforgeOptions {
        accessToken = providers.environmentVariable("CURSE_TOKEN")
        projectId = "${curseforge_id}"
        minecraftVersions.add("${minecraft_version}")
    }

    def modrinthOptions = modrinthOptions {
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")
        projectId = "${modrinth_id}"
        minecraftVersions.add("${minecraft_version}")
    }

    curseforge("curseforgeFabric") {
        from(curseForgeOptions, fabricOptions)
        requires {
            slug = "fabric-api"
        }
    }

//    curseforge("curseforgeForge") {
//        from(curseForgeOptions, forgeOptions)
//    }

    curseforge("curseforgeNeo") {
        from(curseForgeOptions, neoOptions)
    }

    if (providers.environmentVariable("MODRINTH_TOKEN").getOrNull() != null) {
        modrinth("modrinthFabric") {
            from(modrinthOptions, fabricOptions)
            requires {
                slug = "fabric-api"
            }
        }

//        modrinth("modrinthForge") {
//            from(modrinthOptions, forgeOptions)
//        }

        modrinth("modrinthNeo") {
            from(modrinthOptions, neoOptions)
        }
    }

    github {
//        file = project.provider { project(":forge").tasks.remapJar }.flatMap { it.archiveFile }
        file = project.provider { project(":neoforge").tasks.remapJar }.flatMap { it.archiveFile }
        additionalFiles.from project.provider { project(":fabric").tasks.remapJar }.flatMap { it.archiveFile }
        repository = "${github_repo}"
        accessToken = providers.environmentVariable("GITHUB_TOKEN")
        commitish = providers.environmentVariable("GITHUB_SHA").orElse("dryRun")
    }
}